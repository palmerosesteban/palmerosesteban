buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }


    dependencies {
        classpath 'io.fabric.tools:gradle:1.20.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'hugo'//for method logging (only in debug builds)
apply plugin: 'com.github.ben-manes.versions'// to check for dependency updates
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildToolsVersion
    def buildNumber = (System.getenv("BUILD_NUMBER") == null ? 0 : System.getenv("BUILD_NUMBER")) as int


    defaultConfig {
        applicationId "butter.droid"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode buildNumber
        versionName "0.2.5.2"
        multiDexEnabled true
        manifestPlaceholders = [crashlyticsApiKey: System.getenv("CRASHLYTICS_API_KEY") ?: "b246d46ecb3f6b64f3ce24c0c2055cc4fd101c93"]
    }

    signingConfigs {
        mainConfig {
            keyAlias System.getenv("S_ALIAS").toString() ?: ""
            keyPassword System.getenv("S_KEYPASS").toString() ?: ""
            storeFile System.getenv("S_KEYSTORE").toString() ? file(System.getenv("S_KEYSTORE").toString()) : file("")
            storePassword System.getenv("S_KEYPASS").toString() ?: ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.mainConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix ("#" + (buildNumber == 0 ? "local" : buildNumber).toString())
            applicationIdSuffix ".dev"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }

    dexOptions {
        jumboMode true
        javaMaxHeapSize "4g"
    }

    splits {
        abi {
            enable true
            universalApk false
            reset()
            include 'armeabi-v7a', 'x86', 'arm64-v8a'
        }
    }

    project.ext.versionCodes = ["armeabi": 1, "armeabi-v7a": 2, "arm64-v8a": 3, "x86": 4]
}

//to check for dependency updates,
//run ./gradlew dependencyUpdates -Drevision=release
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:support-v4:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:mediarouter-v7:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:support-annotations:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:design:${rootProject.ext.appCompatVersion}"

    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.nirhart:parallaxscroll:1.0'
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.gu:option:1.3'
    compile 'net.rdrei.android.dirchooser:library:3.0@aar'
    compile 'com.github.johnkil.android-robototextview:robototextview:2.4.2'
    
    compile 'com.sothree.slidinguppanel:library:3.1.1'

    compile project(':base')
}
